defaultTasks 'shadowJar'

buildscript {
    ext.kotlin_version = '1.3.10'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.7'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

def getVersionName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    def output = stdout.toString().trim()
    output = output.replaceFirst(/([\d.]+)/, '$1')
    output = output.replaceFirst(/([\d.]+)-(\d+)-.*/, '$1-SNAPSHOT')
    return output
}

task printVersion {
    doLast {
        println("version = ${getVersionName()}")
    }
}

group = 'ru.acapella.db'
version = getVersionName()

repositories {
    mavenCentral()
    jcenter()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

test {
    testLogging.showStandardStreams = true
}

shadowJar {
    classifier = null
    version = null
}

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.6.1'
    compile group: 'io.grpc', name: 'grpc-stub', version: '1.17.1'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.17.1'
    testCompile group: 'junit', name:'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'AcapellaDB gRPC API'
        attributes 'Implementation-Version': version
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0-pre2'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {
                }
            }
            task.builtins {
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    configurations = ['archives']
    pkg {
        repo = 'acapella-db'
        name = 'api-grpc'
        userOrg = 'acapellasoft'
        vcsUrl = 'https://github.com/AcapellaSoft/adb-api-grpc.git'
        licenses = ['Apache-2.0']
        version {
            name = project.version
            desc = 'Acapella DB gRPC API'
            released  = new Date()
            vcsTag = "$project.version"
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

